buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.36.0'
}

apply from: 'docker/docker.gradle'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.16'
    junitVersion = '5.7.0'
    springBootVersion = '2.3.4.RELEASE'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly(
            "org.projectlombok:lombok:$lombokVersion",
    )

    implementation(
            'org.jetbrains:annotations:20.1.0',
            'org.apache.commons:commons-lang3:3.11',
            'commons-io:commons-io:2.8.0',
            'com.google.guava:guava:29.0-jre',
            'com.alibaba:fastjson:1.2.75',
            'cglib:cglib-full:2.0.2',
            'com.rabbitmq:amqp-client:5.10.0',
            'com.rabbitmq:http-client:3.7.0.RELEASE',
            'org.apache.kafka:kafka-clients:2.6.0',
            'org.apache.httpcomponents:httpclient:4.5.13',
            'io.lettuce:lettuce-core:6.0.1.RELEASE',
            'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.0',
            "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
    )

    implementation('org.apache.zookeeper:zookeeper:3.6.2') {
        exclude module: 'slf4j-log4j12'
    }

    runtimeOnly(
            'com.h2database:h2:1.4.200',
    )

    testImplementation(
            'org.assertj:assertj-core:3.18.1',
            'org.awaitility:awaitility:4.0.3',
            'org.junit.platform:junit-platform-launcher:1.7.0',
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion",
            "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
    )

    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
    )
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
}