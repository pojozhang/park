buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

apply from: 'docker/docker.gradle'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.12'
    junitVersion = '5.6.2'
    springBootVersion = '2.3.0.RELEASE'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly(
            "org.projectlombok:lombok:$lombokVersion",
    )

    implementation(
            'org.jetbrains:annotations:19.0.0',
            'org.apache.commons:commons-lang3:3.10',
            'commons-io:commons-io:2.7',
            'com.google.guava:guava:29.0-jre',
            'com.alibaba:fastjson:1.2.72',
            'cglib:cglib-full:2.0.2',
            'com.rabbitmq:amqp-client:5.9.0',
            'com.rabbitmq:http-client:3.7.0.RELEASE',
            'org.apache.kafka:kafka-clients:2.5.0',
            'org.apache.httpcomponents:httpclient:4.5.12',
            'io.lettuce:lettuce-core:5.3.1.RELEASE',
            'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.8.0',
            'org.apache.zookeeper:zookeeper:3.6.1',
            "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
    )

    runtimeOnly(
            'com.h2database:h2:1.4.200',
    )

    testImplementation(
            'org.assertj:assertj-core:3.16.1',
            'org.awaitility:awaitility:4.0.3',
            'org.junit.platform:junit-platform-launcher:1.6.2',
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion",
            "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
    )

    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
    )
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
}